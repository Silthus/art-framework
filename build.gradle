/*
 * Copyright 2020 ART-Framework Contributors (https://github.com/Silthus/art-framework)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'kr.entree.spigradle' version '1.2.1' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()

        maven {
            name = 'Spigot'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven {
            name = "github"
            url = 'https://maven.pkg.github.com/Silthus/ConfigLib'
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }

        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    }

    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.johnrengelman.shadow'

    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/publish.gradle"

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencies {
        compile 'org.projectlombok:lombok:1.+'
        annotationProcessor 'org.projectlombok:lombok:1.+'

        api group: 'com.google.inject', name: 'guice', version: '4.2.3'
        api group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '4.2.3'
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'

        //Test dependencies
        testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
        testImplementation "org.mockito:mockito-core:3.+"
        testImplementation 'org.assertj:assertj-core:3.+'
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.+'
        testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.+'
        testImplementation 'org.projectlombok:lombok:1.+'
        testAnnotationProcessor 'org.projectlombok:lombok:1.+'
    }

    shadowJar {
        classifier = ''
        dependencies {
            include(dependency('com.google.inject:guice:'))
            include(dependency('org.apache.commons:commons-lang3:'))
        }
        relocate 'com.google.injection', "${packageName}.guice"
        relocate 'com.apache.commons', "${packageName}.commonslang"
    }

    tasks.build.dependsOn(shadowJar)
    tasks.publish.dependsOn(shadowJar)

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

configure(subprojects.findAll { it.name == 'art-bukkit' || it.name == 'art-example' }) {

    apply plugin: 'kr.entree.spigradle'

    spigot {
        name = "${project.property("pluginName")}"
        authors = [project.property("author")]
        apiVersion = project.property("apiVersion")
        load = STARTUP
    }

    dependencies {
        implementation "org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT"
    }

    task copyPluginYaml(type: Copy, dependsOn: spigotPluginYaml) {
        from(new File(spigotPluginYaml.temporaryDir, "plugin.yml"))
        into(sourceSets.test.resources.srcDirs[0])
    }
}

defaultTasks 'build'