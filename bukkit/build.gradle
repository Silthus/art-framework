/*
 * Copyright 2020 ART-Framework Contributors (https://github.com/Silthus/art-framework)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'kr.entree.spigradle' version '2.1.1'
    id 'io.ebean' version '12.6.5'
}

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

ext {
    mcVersion = project.property("mcVersion")
}

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    softDepends = ['ebean-wrapper', 'PlaceholderAPI']
}

archivesBaseName = "${project.property("pluginName")}-bukkit"

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    implementation spigot(mcVersion)
    implementation 'net.silthus:ebean-wrapper:2.8.1'
    implementation 'com.google.code.gson:gson:2.8.6'

    api project(':api')
    implementation project(':art-scripts')
    implementation 'io.art-framework:art-placeholderapi:1.0.1'

    testImplementation 'io.ebean:ebean-test:12.6.5'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.16:0.20.0'

    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
    testImplementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.1'
    testImplementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.22'
    testImplementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
}

shadowJar {
    classifier = ''
    dependencies {
        include(project(':api'))
        include(project(':art-scripts'))
        include(dependency('io.art-framework:art-placeholderapi:'))
        include(dependency('com.google.code.gson:gson:'))
    }
    relocate 'com.google.gson', "shadow.${packageName}.gson"
}

tasks.build.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)
