/*
 * Copyright 2020 ART-Framework Contributors (https://github.com/Silthus/art-framework)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0' apply false
    id "io.freefair.lombok" version "5.1.0"
    id 'java-library'
    id 'jacoco'
    id 'idea'
}

allprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'idea'
    apply plugin: 'io.freefair.lombok'

    apply from: "$rootDir/gradle/jacoco.gradle"
    apply from: "$rootDir/gradle/publish.gradle"

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        implementation group: "com.google.guava", name: "guava", version: "29.0-jre"
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
        implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.2'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.11.2'
        implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.2'
        implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.2'

        //Test dependencies
        testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
        testImplementation "org.mockito:mockito-core:3.+"
        testImplementation 'org.assertj:assertj-core:3.+'
        testImplementation 'org.awaitility:awaitility:4.0.3'
        testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.+'
        testImplementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.+'
    }

    shadowJar {
        classifier = ''
        dependencies {
            include(dependency('org.apache.commons:commons-lang3:'))
            include(dependency('com.google.guava:guava:'))
            include(dependency('org.reflections:reflections:'))
            include(dependency('com.fasterxml.jackson.core::'))
            include(dependency('com.fasterxml.jackson.dataformat::'))
        }
        relocate 'org.apache.commons', "shadow.${packageName}.commonslang"
        relocate 'com.google.guava', "shadow.${packageName}.guava"
        relocate 'org.reflections', "shadow.${packageName}.reflections"
        relocate 'com.fasterxml.jackson', "shadow.${packageName}.jackson"
    }

    tasks.build.dependsOn(shadowJar)
    tasks.publish.dependsOn(shadowJar)

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }

    defaultTasks 'build'
}